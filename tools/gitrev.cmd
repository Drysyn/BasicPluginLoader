@echo off

set DIR=%~dp0
set LASTREV=%DIR%lastrev.txt
set TARGETDIR=%DIR%\..\src\loader

IF NOT EXIST %LASTREV% (
    echo 0 > %LASTREV%
)

:: in case unix utilities are on the PATH prior to windows utilities
set FIND=%systemroot%\system32\find.exe

for /f "tokens=1" %%g in ('git rev-list HEAD ^| %FIND% /c /v ""') do set REVCOUNT=%%g
for /f "tokens=1" %%g in ('git describe --always --dirty') do set REVISION=%%g

FOR /F %%H IN (%LASTREV%) DO SET LASTREVISION=%%H

echo Current revision is %REVISION%
echo Last revision is %LASTREVISION%

IF %LASTREVISION% NEQ %REVISION% (
    echo // generated by gitrev.cmd > %TARGETDIR%\buildnumber.h
    echo #define BUILDNUMBER %REVCOUNT% >> %TARGETDIR%\buildnumber.h
    echo #define BUILDNUMBER_STR "%REVISION%-%REVCOUNT%" >> %TARGETDIR%\buildnumber.h
    echo #define BUILDHOST "%USERNAME%@%COMPUTERNAME%" >> %TARGETDIR%\buildnumber.h
    echo %REVISION% > %LASTREV%

    echo Updated revision
)
